#!/usr/bin/perl
#
# Run checks on a Coraid AoE disk array using Coraid's 'cec' utility.
#
# Ideas stolen from Erwan Ben Souiden's check_coraid.pl, which I liked, but didn't
# want to work for me (and the use of script output files seems overly fragile).
#

use strict;

use Nagios::Plugin;
use Expect;

# %check is a map defining checks as a command to run, and a sub to run on the command output
my %check = (
  # 'disks' check now deprecated, since up/down info no longer available there
  disks         => [ 
    'show -l' => sub {
      my ($np, $data) = @_;
      $np->nagios_exit( UNKNOWN, "'disks' check no longer supported" );
    }
  ],
  raid          => [ 
    'list -l' => sub {
      my ($np, $data) = @_;
      $np->nagios_die( "'raid' check returned no data" ) unless $data;

      my $found = 0;
      while ($data =~ m/^ \s+ ( (\S+) \s+ (\S+) \s+ (raid\w+) \s+ ([\w,]+) ) \s* $/msgx) {
        my ($line, $lun, $size, $raid, $state) = ($1, $2, $3, $4, $5);
        $line =~ s/\s\s+/ /g;
        my $result = $state =~ qr/failed|degraded/ ? CRITICAL :
                     $state eq 'normal'            ? OK :
                                                     WARNING;
        $np->add_message( $result, "LUN $line" );
        $found++;
      }
      $np->nagios_exit( UNKNOWN, "no raid output found" ) if $found == 0;
    }
  ],
  spares        => [
    'spare' => sub {
      my ($np, $data) = @_;
      chomp $data;

      # Remove stuff up to and including command
      my @data = split /\n/, $data;
      shift @data while @data and $data[0] !~ m/^spare/;
      shift @data while @data and $data[0] =~ m/^spare/;

      # Return ok if #spares > 0
      if (@data) {
        $np->add_message( OK, scalar(@data) . " spares found: " .
          join(',', map { s/\W+$//; $_ } @data) );
      }
      else {
        $np->add_message( CRITICAL, "No spares found" );
      }
    },
  ],
);
my $checks = join ' | ', sort keys %check;

my $cmd_timeout = 2;

my $np = Nagios::Plugin->new(
  usage         => "Usage: %s [-s <shelf>] -i <interface> -k <check>\n" .
                   "  where <check> is one of: raid | spares\n",
  version       => '0.2',
  url           => 'https://github.com/gavincarr/nagios-of-plugins',
  blurb         => q{This plugin runs checks on a Coraid AoE disk array using Coraid's cec utility
(which is expected to be in your path).},
);

$np->add_arg(
  spec          => 'shelf|s=i',
  help          => '-s, --shelf=INTEGER
   Shelf to check (integer, 1 - 99)',
  default       => 1,
);
$np->add_arg(
  spec          => 'interface|i=s',
  help          => '-i, --interface=STRING
   Network interface to be used',
  required      => 1,
);
$np->add_arg(
  spec          => 'check|k=s@',
  help          => "-k, --check=[ $checks ]
   Check to run via cec, valid values: 'raid' | 'spares' (may repeat)",
  required      => 1,
);

$np->getopts;
my $shelf       = $np->opts->shelf;
my $interface   = $np->opts->interface;
my $checklist   = $np->opts->check;
foreach (@$checklist) {
  $np->die("Invalid check '$_'") unless $check{$_};
}

alarm $np->opts->timeout;
my $exp = Expect->new;
$exp->raw_pty(1);
$exp->log_stdout($np->opts->verbose);
$exp->spawn('cec', '-s', $shelf, $interface);

$exp->expect($cmd_timeout, -re => "Escape is");

for my $checktype (@$checklist) {
  my ($cmd, $sub) = @{ $check{$checktype} };
  $exp->send("$cmd\n");
  my ($pos, $error, $match, $data) = $exp->expect(undef, -re => "SRX? shelf $shelf> ");
  $np->die($error) if $error;
  $sub->($np, $data);
}

# Exit
$exp->send("");
$exp->expect($cmd_timeout, ">>> ");
$exp->send("q\n");
$exp->expect($cmd_timeout, 'EOF');

$exp->soft_close;

$np->nagios_exit( $np->check_messages( join => ', ' ) );

__END__

=todo 

- add volume offline checking (# online)
- add disk checking (# of disks up)
- add --slot support

=cut

