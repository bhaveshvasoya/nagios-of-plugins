#!/usr/bin/perl -w
#
# check_interface - plugin to check the state of a network interface.
#

use strict;
use File::Basename;
use IO::Interface::Simple;
use Nagios::Plugin 0.1301;

my $np = Nagios::Plugin->new(
  usage => qq(Usage: %s [--up|--down] [--ip <ipaddr>] [--netmask <mask>] [--mtu <mtu>]
                        [--hwaddr <hwaddr>] [--ptp] [--ptp-pingable] <interface>),
  version => '0.02',
  url => 'https://github.com/gavincarr/nagios-of-plugins',
  blurb => qq(This plugin checks the state of a network interface.),
);

$np->add_arg(
  spec => "up|U",
  help => q{-U, --up
   Assert that interface is up (checked by default, unless --down specified).});
$np->add_arg(
  spec => "down|D",
  help => q(-D, --down
   Assert that interface is down.));
$np->add_arg(
  spec => "ipaddr|ip|i=s",
  help => q(-i, --ip, --ipaddr=IPADDR
   IP address that should be assigned to this interface.));
$np->add_arg(
  spec => "netmask=s",
  help => q(--netmask=NETMASK
   Netmask that should apply to this interface.));
$np->add_arg(
  spec => "mtu=s",
  help => q(--mtu=MTU
   MTU that should apply to this interface.));
$np->add_arg(
  spec => "hwaddr=s",
  help => q(--hwaddr=HWADDR
   Hardware/MAC address that should be attached to this interface.));
$np->add_arg(
  spec => "ptp|P",
  help => q(-P, --ptp
   Assert that interface is a pointtopoint interface.));
$np->add_arg(
  spec => "ptp-pingable",
  help => q(--ptp-pingable
   Assert that interface is a pointtopoint interface and peer is pingable.));

$np->getopts;

$np->nagios_exit("UNKNOWN", "no interface specified") 
  if ! @ARGV;
$np->nagios_exit("UNKNOWN", "multiple interfaces specified") 
  if @ARGV > 1;
$np->nagios_exit("UNKNOWN", "cannot specify both --up and --down")
  if $np->opts->up and $np->opts->down;

# Setup
my $interface = shift @ARGV;
my $status = "CRITICAL";
my $if = IO::Interface::Simple->new($interface)
  or $np->nagios_exit("UNKNOWN", "invalid interface $interface");

# Check up/down (--up is on by default unless --down is specified)
if ($np->opts->down) {
  $np->nagios_exit($status, "interface $interface is UP") if $if->is_running;
} else {
  $np->nagios_exit($status, "interface $interface is DOWN") unless $if->is_running;
}

# Check ip
if ($np->opts->ipaddr) {
  $np->nagios_exit($status, "interface $interface has NO ip address, not " . $np->opts->ipaddr)
      if ! $if->address;
  $np->nagios_exit($status, "interface $interface has address " . $if->address . 
    ", not " . $np->opts->ipaddr)
      if $if->address ne $np->opts->ipaddr;
}

# Check netmask
if ($np->opts->netmask) {
  $np->nagios_exit($status, "interface $interface has NO netmask, not " . $np->opts->netmask)
      if ! $if->netmask;
  $np->nagios_exit($status, "interface $interface has netmask " . $if->netmask . 
    ", not " . $np->opts->netmask)
      if $if->netmask ne $np->opts->netmask;
}

# Check MTU
if ($np->opts->mtu) {
  $np->nagios_exit($status, "interface $interface has NO mtu, not " . $np->opts->mtu)
      if ! $if->mtu;
  $np->nagios_exit($status, "interface $interface has mtu " . $if->mtu . 
    ", not " . $np->opts->mtu)
      if $if->mtu ne $np->opts->mtu;
}

# Check hwaddr
if ($np->opts->hwaddr) {
  $np->nagios_exit($status, "interface $interface has NO hardware/mac address, not " . $np->opts->hwaddr)
      if ! $if->hwaddr;
  $np->nagios_exit($status, "interface $interface has hardware/mac address " . $if->hwaddr . 
    ", not " . $np->opts->hwaddr)
      if $if->hwaddr ne $np->opts->hwaddr;
}

# Check ptp
if ($np->opts->ptp || $np->opts->get('ptp-pingable')) {
  $np->nagios_exit($status, "interface $interface is NOT point-to-point")
    if ! $if->is_pt2pt;
}

# Check ptp-pingable
if ($np->opts->get('ptp-pingable')) {
  my $dstaddr = $if->dstaddr 
    or die $np->nagios_exit($status, "interface $interface has no peer address - cannot ping");
  require Net::Ping;
  my $p = Net::Ping->new;
  $p->ping($dstaddr) 
    or die $np->nagios_exit($status, "interface $interface peer $dstaddr is NOT pingable");
}

$np->nagios_exit("OK", "interface $interface ok");

# vim:ft=perl
